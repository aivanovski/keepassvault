apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
	def ext = rootProject.extensions.ext

	compileSdkVersion ext.android.compileSdk
	buildToolsVersion ext.android.buildTools

	defaultConfig {
		applicationId 'com.ivanovsky.passnotes'
		minSdkVersion ext.android.minSdk
		targetSdkVersion ext.android.targetSdk
		versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
		testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
		vectorDrawables.useSupportLibrary true
		multiDexEnabled true

		buildConfigField "String[]", "DEBUG_FILE_NAME_PATTERNS", "null"
		buildConfigField "String[]", "DEBUG_PASSWORDS", "null"

		buildConfigField "String", "DROPBOX_APP_KEY", "$ext.dropboxAppKey"
		buildConfigField "String", "DROPBOX_APP_SECRET", "$ext.dropboxAppSecret"
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		androidTest.java.srcDirs += 'src/androidTest/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	signingConfigs {
		debug {
			storeFile file("../keys/debug.keystore")
		}
		release {
			storeFile file("../keys/release.keystore")
			storePassword "zimber88"
			keyAlias "alexei.ivanovski"
			keyPassword "zimber88"
		}
	}

	buildTypes {
		debug {
			debuggable true
			minifyEnabled false
			signingConfig signingConfigs.debug

			if (ext.debugFileNameToPasswordMap != null) {
				buildConfigField "String[]", "DEBUG_FILE_NAME_PATTERNS", "{" +
						ext.debugFileNameToPasswordMap.keySet().collect{"\"${it}\""}.join(",") +
						"}"
				buildConfigField "String[]", "DEBUG_PASSWORDS", "{" +
						ext.debugFileNameToPasswordMap.values().collect{"\"${it}\""}.join(",") +
						"}"
			}
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	testOptions {
		unitTests {
			includeAndroidResources = true
		}
	}

	kapt {
		generateStubs = true
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	// Test dependencies
	testImplementation "junit:junit:$jUnitVersion"
	testImplementation "org.mockito:mockito-core:$mockitoVersion"
	testImplementation "com.google.truth:truth:0.42"//TODO: move version to configuration
	testImplementation "org.robolectric:robolectric:$robolectricVersion"
	testImplementation "org.robolectric:shadows-multidex:$robolectricVersion"

	// Android test dependencies
	androidTestImplementation 'androidx.test.ext:junit:1.1.0'//TODO: move version to configuration
	androidTestImplementation "androidx.test:rules:1.1.0"
	androidTestImplementation "androidx.room:room-testing:2.0.0"
	androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$compiler.kotlin"

	// MultiDex
	implementation "androidx.multidex:multidex:2.0.0"

	// Dagger
	implementation "com.google.dagger:dagger:$daggerVersion"
	kapt "com.google.dagger:dagger-compiler:$daggerVersion"

	// Room
	implementation "androidx.room:room-runtime:2.0.0"
	kapt "androidx.room:room-compiler:2.0.0"
	androidTestImplementation "androidx.room:room-testing:2.0.0"

	// Lifecycle libraries
	implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
	kapt "androidx.lifecycle:lifecycle-compiler:2.0.0"
	testImplementation "androidx.arch.core:core-testing:2.0.0"

	// Support libraries
	implementation "androidx.appcompat:appcompat:1.0.0"
	implementation "com.google.android.material:material:1.0.0"
	implementation "androidx.legacy:legacy-support-v4:1.0.0"
	implementation "androidx.recyclerview:recyclerview:1.0.0"
	implementation "androidx.cardview:cardview:1.0.0"
	implementation "androidx.annotation:annotation:1.0.0"
	implementation "androidx.constraintlayout:constraintlayout:1.1.2"

	// Rx
	implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
	implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

	// Java stream API
	implementation "com.annimon:stream:$annimonStreamApiVersion"

	//Apache Commons Lang
	implementation "org.apache.commons:commons-lang3:$apacheCommonsLangVersion"

	// Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$compiler.kotlin"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$compiler.kotlin"

	// KeePassJava2
	implementation project(':libs:KeePassJava2-database')
	implementation project(':libs:KeePassJava2-kdbx')
	implementation project(':libs:KeePassJava2-simple')

	// Dropbox
	implementation "com.dropbox.core:dropbox-core-sdk:$dropboxVersion"

	// OkHttp
	implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"

	// Stetho
	implementation "com.facebook.stetho:stetho:1.5.1"

	// Completable Future
	implementation "net.sourceforge.streamsupport:android-retrofuture:1.7.1"
}
