import groovy.json.JsonSlurper

// Returns map which contains file name patterns (without file extension) and passwords for
// password autocomplete in Unlock screen.
// Emails and passwords should be written in property named 'debugPasswordRules'
// in file 'debug.properties' in the format as follows:
//
//debugPasswordRules={ "[a-zA-Z0-9]*test[a-zA-Z0-9]*":"abc23" }

def PROPERTY_DEBUG_PASSWORD_RULES = 'debugPasswordRules'
def PROPERTY_DROPBOX_APP_KEY = 'dropboxAppKey'
def PROPERTY_DROPBOX_APP_SECRET = 'dropboxAppSecret'
def KEYSTORE_PASSWORD = "password"
def KEYSTORE_ALIAS = "alias"

def parsePropertiesMap = { ->
	def result = [:]

	Properties properties = new Properties()
	def propertiesFile = project.file('debug.properties')

	if (propertiesFile.canRead()) {
		properties.load(new FileInputStream(propertiesFile))

		for (String propertyName : properties.stringPropertyNames()) {
			def propertyValue

			if (propertyName == PROPERTY_DEBUG_PASSWORD_RULES) {
				def passwordRulesJson = properties[PROPERTY_DEBUG_PASSWORD_RULES]
				def rules = new JsonSlurper().parseText(passwordRulesJson)

				assert rules instanceof Map

				rules.each { fileName, password ->
					project.logger.lifecycle("Use password $password for file with name matched $fileName")}

				propertyValue = rules
			} else {
				propertyValue = properties[propertyName]
			}

			result[propertyName] = propertyValue
		}
	}

	return result
}

def parseKeystoreProperties = {
	def result = [:]

	def propertiesFile = project.file('keys/keystore.properties')

	if (propertiesFile.canRead()) {
		Properties properties = new Properties()
		properties.load(new FileInputStream(propertiesFile))
        result.putAll(properties)
	}

	return result
}

def propertiesMap = parsePropertiesMap()
def keystorePropertiesMap = parseKeystoreProperties()

ext {
	debugFileNameToPasswordMap = propertiesMap[PROPERTY_DEBUG_PASSWORD_RULES]
	dropboxAppKey = propertiesMap[PROPERTY_DROPBOX_APP_KEY]
	dropboxAppSecret = propertiesMap[PROPERTY_DROPBOX_APP_SECRET]
	keystorePassword = keystorePropertiesMap[KEYSTORE_PASSWORD]
	keystoreAlias = keystorePropertiesMap[KEYSTORE_ALIAS]

	android = [
			minSdk    : 17,
			targetSdk : 29,
			compileSdk: 30,
			buildTools: '30.0.0'
	]

	kotlinVersion = '1.3.71'

	// KeePassJava 2 dependencies
	jetbrainsAnnotationsVersion = '15.0'
	guavaVersion = '24.0-android'
	spongyCastleVersion = '1.58.0.0'
	simpleXmlVersion = '2.7.1'
	aaltoXmlVersion = '1.0.0'

}


