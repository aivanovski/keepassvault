apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def versionMajor = 0
def versionMinor = 1
def versionPatch = 0

android {
	def ext = rootProject.extensions.ext

	compileSdkVersion 26
	buildToolsVersion '26.0.2'

	defaultConfig {
		applicationId 'com.ivanovsky.passnotes'
		minSdkVersion 14
		targetSdkVersion 26
		versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
		versionName "${versionMajor}.${versionMinor}.${versionPatch}"
		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
		vectorDrawables.useSupportLibrary true
		multiDexEnabled true

		buildConfigField "String[]", "DEBUG_FILE_NAME_PATTERNS", "null"
		buildConfigField "String[]", "DEBUG_PASSWORDS", "null"
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}

	dataBinding {
		enabled = true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	signingConfigs {
		debug {
			storeFile file("../keys/debug.keystore")
		}
		release {
			storeFile file("../keys/release.keystore")
			storePassword "zimber88"
			keyAlias "alexei.ivanovski"
			keyPassword "zimber88"
		}
	}

	buildTypes {
		debug {
			debuggable true
			minifyEnabled false
			signingConfig signingConfigs.debug

			if (ext.debugFileNameToPasswordMap != null) {
				buildConfigField "String[]", "DEBUG_FILE_NAME_PATTERNS", "{" +
						ext.debugFileNameToPasswordMap.keySet().collect{"\"${it}\""}.join(",") +
						"}"
				buildConfigField "String[]", "DEBUG_PASSWORDS", "{" +
						ext.debugFileNameToPasswordMap.values().collect{"\"${it}\""}.join(",") +
						"}"
			}
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	kapt {
		generateStubs = true
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	testImplementation 'junit:junit:4.12'

	//Dagger
	kapt 'com.google.dagger:dagger-compiler:2.11'
	implementation 'com.google.dagger:dagger:2.11'

	//Room
	kapt 'android.arch.persistence.room:compiler:1.1.1'
	implementation 'android.arch.persistence.room:runtime:1.1.1'

	//Support libraries
	implementation 'com.android.support:appcompat-v7:26.1.0'
	implementation 'com.android.support.constraint:constraint-layout:1.1.2'
	implementation 'com.android.support:design:26.1.0'
	implementation 'com.android.support:support-v4:26.1.0'
	implementation 'com.android.support:recyclerview-v7:26.1.0'
	implementation 'com.android.support:support-annotations:26.1.0'

	//Java stream api
	implementation 'com.annimon:stream:1.1.4'

	//Rx
	implementation 'io.reactivex.rxjava2:rxjava:2.0.6'
	implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

//	Google Drive API
//	implementation 'com.google.android.gms:play-services-drive:11.8.0'
//	implementation 'com.google.android.gms:play-services-auth:11.8.0'

	//KeePassJava2
	implementation project(':libs:KeePassJava2-database')
	implementation project(':libs:KeePassJava2-kdbx')
	implementation project(':libs:KeePassJava2-simple')

	//MultiDex
	implementation 'com.android.support:multidex:1.0.3'

	//Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.51"
	kapt 'com.android.databinding:compiler:3.0.1'
}
